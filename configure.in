## smbldap-tools configure.in
## ======================================================================

AC_PREREQ(2.50)

AC_INIT(smbldap-tools, 0.9.6.svn)

AC_PREFIX_DEFAULT(/usr/local)

#AC_PROG_INSTALL

package_subdir="$PACKAGE_NAME"
PERL_VERSION=5.008001
SAMBA_SYSCONFDIR="/etc/samba"
SAMBA_BINDIR="/usr/bin"

## Installation directories
## ======================================================================

AC_SUBST(package_subdir)

AC_ARG_WITH(
  subdir,
  [AC_HELP_STRING(--with-subdir=DIR,
    [change default subdirectory used for installs])],
  [package_subdir="$withval"],
)

case "$package_subdir" in
yes|no|"")
    package_subdir=""
    ;;
*)
    AC_MSG_RESULT([using $package_subdir for install subdir])
    package_subdir="/$package_subdir"
    ;;
esac

## Perl
## ======================================================================

AC_SUBST(PERL_CMD)

AC_ARG_WITH(
    perl,
    [AC_HELP_STRING(--with-perl=PATH, [Use specific perl command])],
    [PERL_CMD="$withval"],
)

case "$PERL_CMD" in
/*)
    ;;
*)
    AC_PATH_PROG(PERL_CMD, perl, not found)
    ;;
esac

AC_MSG_RESULT([using $PERL_CMD for perl])

## ----------------------------------------------------------------------

AC_MSG_CHECKING(for Perl version)

if test x`"$PERL_CMD" -e "use $PERL_VERSION; print 'ok'"` = x"ok"; then
    AC_MSG_RESULT($PERL_VERSION or later)
else
    AC_MSG_ERROR(Perl $PERL_VERSION or later required.)
fi

## ----------------------------------------------------------------------

AC_SUBST(PERL_LIBDIR)

AC_ARG_WITH(
    perl-libdir,
    [AC_HELP_STRING(--with-perl-libdir=PATH,[*.pm install directory])],
    [PERL_LIBDIR="$withval"],
)

case "$PERL_LIBDIR" in
/*)
    ;;
sitelib)
    PERL_LIBDIR=`$PERL_CMD -MConfig -e 'print $Config{installsitelib}'`
    ;;
*)
    PERL_LIBDIR=`$PERL_CMD -MConfig -e 'print $Config{installvendorlib}'`
    ;;
esac

AC_MSG_RESULT([using $PERL_LIBDIR for *.pm install directory])

## Samba
## ======================================================================

AC_SUBST(SAMBA_SYSCONFDIR)

AC_ARG_WITH(
    samba-sysconfdir,
    [AC_HELP_STRING(--with-samba-sysconfdir=DIR,
	[Path for Samba sysconf directory])],
    [SAMBA_SYSCONFDIR="$withval"],
)

AC_MSG_RESULT([using $SAMBA_SYSCONFDIR for Samba sysconf directory])

## ----------------------------------------------------------------------

AC_SUBST(SAMBA_BINDIR)

AC_ARG_WITH(
    samba_bindir,
    [AC_HELP_STRING([--with-samba-bindir=DIR],
	[Path for Samba bin directory])],
    [SAMBA_BINDIR="$withval"],
)

AC_MSG_RESULT([using $SAMBA_BINDIR for Samba bin directory])

## Output
## ======================================================================

AC_CONFIG_FILES(
    Makefile
    build/Makefile
    build/Makefile.package
    build/subst.pl
)

AC_OUTPUT

